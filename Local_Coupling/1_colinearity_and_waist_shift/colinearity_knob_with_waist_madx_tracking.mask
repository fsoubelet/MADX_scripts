option, -echo, -warn;
!system, "ln -nfs ~/cernbox/OMC/MADX_scripts/Local_Coupling scripts";  ! for local
system, "ln -nfs /afs/cern.ch/work/f/fesoubel/MADX_scripts/Local_Coupling scripts";  ! for afs / htcondor

!#############################################################################
!############################## Tune and chroma ##############################
!#############################################################################

! Tune definitions used in some of the later macros
qx0 = 62.31;
qy0 = 60.32;
tsplit = 2.0;
qx00 = 62.0;
qy00 = 60.0;
qprime = 2.0;

qxinit = qx0;
qyinit = qy0;
qxfinal = 62.31; ! Define initial qxinit / qyinit with larger separation for robustness to large coupling errors as sources are introduced (want to avoid getting stuck on the wrong side of qx=qy)
qyfinal = 60.32; ! Then rematch to desired final tunes at the very end of the script. qx0 etc are kept with the init values for coupling routines etc.

ptcqxinit = qxinit - qx00;
ptcqyinit = qyinit - qy00;
qxinitint = qxinit - ptcqxinit;
qyinitint = qyinit - ptcqyinit;

dqxinit = qprime;
dqyinit = qprime;
dqxfinal = qprime;
dqyfinal = qprime;

!vrf400 = 6;  ! Getting RF powering for sixtracklib tracking later on
!lagrf400.b1 = 0.5;  ! Cavity phase difference in units of 2pi
!lagrf400.b2 = 0;

!#############################################################################
!############################# Setting Up Lattice ############################
!#############################################################################

call, file="scripts/lhc_as-built.seq";
beam;

slicefactor = 8;
call, file="scripts/myslice.madx";
beam;

use, sequence=lhcb1; makethin, sequence=lhcb1, style=teapot, makedipedge=true;

! Cycling w.r.t. to IP5 (that we want to compare to IP1 which is affected by local coupling)
seqedit, sequence=lhcb1; flatten; cycle, start=IP5; endedit;
use, sequence=lhcb1;

!=> opticsfile.22 is with betastar 30cm at IP1 and IP5
call, file="scripts/opticsfile.22";


!#######################################################################################################
!############################### Setup for orbit and error corrections #################################
!###### Xing scheme should be disabled at start to simplify inclusion of errors/alignments/orbit #######
!#######################################################################################################

on_x1 = 0; on_sep1 = 0; on_o1 = 0;
on_x2 = 0; on_sep2 = 0; on_o2 = 0; on_oe2 =0; on_a2 = 0 ;
on_x5 = 0; on_sep5 = 0; on_o5 = 0;
on_x8 = 0; on_sep8 = 0; on_o8 = 0; on_a8 = 0;  on_sep8h = 0; on_x8v = 0;

on_alice = 0;
on_sol_alice = 0;
on_lhcb = 0;
on_sol_atlas = 0;
on_sol_cms = 0;

NRJ = 6500;
brho := NRJ * 1e9 / clight;
geometric_emit := 3.75e-6 / (NRJ / 0.938); ! This is geometrical emittance.

! Set up beam
beam, bv=1, energy=NRJ, particle=proton, npart=1.0e10, kbunch=1, ex=geometric_emit, ey=geometric_emit, et=0.0000;
use, sequence=lhcb1;

! Match Tunes and Chromaticity
match, chrom;
  global, Q1=qx0, Q2=qy0;
  global, DQ1=qprime, DQ2=qprime;
  vary, name=dQx.b1_sq;
  vary, name=dQy.b1_sq;
  vary, name=dQpx.b1_sq;
  vary, name=dQpy.b1_sq;
  LMDIF, calls=100, tolerance=1.0E-21;
endmatch;

use, sequence=lhcb1;

! ================================ !
!  Create and Export Distribution  !
! ================================ !

! A twiss is necessary before distribution command, or it will fail
select, flag=twiss, clear;
select, flag=twiss, pattern="IP";
twiss, file="Outputdata/twiss_for_distribution.tfs";

! Create this here BEFORE affecting things by applying the knob, will be used to initialize particles for tracking
DISTRIBUTION,  horizontal=gauss, vertical=gauss, longitudinal=zero, table=mydist, npart = 100000, cutsig_h={0,3}, cutsig_v={0,3};
write, table=mydist, file="Outputdata/distribution.tfs";  ! should write for any case!

! ============================ !
!  Implement Colinearity Knob  !
! ============================ !

! Save strength of MQSX IP coupling correctors
KQSX3.R1_old = KQSX3.R1;
KQSX3.L1_old = KQSX3.L1;

! Power MQSX for coupling bump
colinearity_knob = %(COLIN_KNOB)s;  ! In units of the colin knob (positive integers only)
KQSX3.R1 = colinearity_knob * 1E-4;
KQSX3.L1 = - colinearity_knob * 1E-4;

! ===================================== !
!  Implement Rigidity Waist Shift Knob  !
! ===================================== !

! Save current value of triplets powering
kqx.l1_old = kqx.l1;
kqx.r1_old = kqx.r1;

! Change Triplets powering for waist shift
rigidity_waist_shift_knob = %(RIGIDITY_WAIST_SHIFT_KNOB)s;  ! In units of the rigidity waist shift knob (positive integers only)
kqx.l1 = (1 + rigidity_waist_shift_knob *.005) * kqx.l1_old;
kqx.r1 = (1 - rigidity_waist_shift_knob *.005) * kqx.r1_old;

! Match Tunes and Chromaticity to compensate the second order contribution of coupling
match, chrom;
  global, Q1=qx0, Q2=qy0;
  global, DQ1=qprime, DQ2=qprime;
  vary, name=dQx.b1_sq;
  vary, name=dQy.b1_sq;
  vary, name=dQpx.b1_sq;
  vary, name=dQpy.b1_sq;
  LMDIF, calls=100, tolerance=1.0E-21;
endmatch;

! Twiss results to output file
select, flag=twiss, clear;
select, flag=twiss, pattern="IP", column=name, s, beta11, beta12, beta21, beta22, r11, r12, r21, r22;
twiss, file="Outputdata/colin_waist_dqmin.tfs", ripken=True;

! ========================== !
!  Get Closest Tune Approach !
! ========================== !

qfmid = (ptcqyinit + ptcqxinit) / 2.0;
qxmid = qx00 + qfmid;
qymid = qy00 + qfmid;

value, qfmid, qxmid, qymid;

! Save previous values
dQx.b1_init=dQx.b1;
dQy.b1_init=dQy.b1;

! Try to match on the same fractional tune, will fail at Cminus
match;
  global, q1=62.295, q2=60.295;
  vary,   name=dQx.b1, step=1.0E-7 ;
  vary,   name=dQy.b1, step=1.0E-7 ;
  LMDIF,  calls=100, tolerance=1.0E-21;
endmatch;

! Put back the previous values
dQx.b1=dQx.b1_init;
dQy.b1=dQy.b1_init;

! Value of the Cminus
dqminacorr = table(summ, q1) - table(summ, q2) - (qx00 - qy00);
twiss;


! =============================== !
!  Particle distribution tracking !
! =============================== !

! Initiate 1000 particles
i = 1;
while (i < 1000) {
  setvars, table=mydist, row=i;
  value, i, x, py;
  track, ONEPASS=false, file="Outputdata/track";!, onetable;
  start, x=x, px=px, y=y, py=py, t=t, pt=pt;
  i = i+1;
}

! Set obervation point at IP1 and run tracking
observe, place=ip1;
run, turns=50;

! ============================================= !
!  Output Interesting Values for Python Scripts !
! ============================================= !

value, colinearity_knob;
value, rigidity_waist_shift_knob;
value, dqminacorr;

system, "unlink scripts";
